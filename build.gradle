buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
    }
}

apply plugin: "java-library"
apply plugin: "idea"
apply plugin: "maven-publish"
apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "signing"

group 'org.grails'
version projectVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
    compileOnly "jakarta.servlet:jakarta.servlet-api:6.0.0"
    compileOnly "jakarta.servlet.jsp:jakarta.servlet.jsp-api:3.1.0"
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

project.tasks.withType(Jar) {
    configure {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

javadoc {
    failOnError = false
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from(components.java)

            pom {
                name = 'Sitemesh'
                description = 'SiteMesh is a web-page layout and decoration framework and web-application integration framework to aid in creating large sites consisting of many pages for which a consistent look/feel, navigation and layout scheme is required.'
                url = 'https://github.com/sitemesh/sitemesh2'
                licenses {
                    license {
                        name =  'The Apache Software License, Version 1.1'
                        url = 'https://raw.githubusercontent.com/sitemesh/sitemesh2/master/LICENSE.txt'
                        distribution =  'repo'
                    }
                }
                developers {
                    developer {
                        id = 'joewalnes'
                        name = 'Joe Walnes'
                        email = 'joe dot walnes at gmail'
                    }
                    developer {
                        id = 'codeconsole'
                        name = 'Scott Murphy'
                    }
                }
                organization {
                    name = 'OpenSymphony'
                }
                scm {
                    connection = 'scm:git:git://github.com/sitemesh/sitemesh2.git'
                    developerConnection = 'scm:git:git@github.com:sitemesh/sitemesh2.git'
                    url = 'https://github.com/sitemesh/sitemesh2/'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/sitemesh/sitemesh2/issues'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}